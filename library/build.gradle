/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

group = 'com.chrisplus.rootmanager'
version = project.version_name

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        versionCode project.version_code.toInteger()
        versionName project.version_name
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                name project.repo_name
                description project.repo_desc
                url project.repo_site

                packaging 'aar'
                artifactId 'library'
                version project.version_name

                // Set your license
                licenses {
                    license {
                        name project.license_name
                        url project.license_url
                    }
                }
                developers {
                    developer {
                        id project.developer_id
                        name project.developer_name
                        email project.developer_email
                    }
                }
                scm {
                    connection project.repo_git
                    developerConnection project.repo_site
                    url project.repo_site

                }
            }
        }
    }
}

bintray {
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = project.repo_name
        websiteUrl = project.repo_site
        vcsUrl = project.repo_git
        licenses = [project.license_name]
        publish = true
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

def Properties props = new Properties()
def propFile = new File('publish.properties')
if (propFile.isFile() && propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('bintray.user') &&
            props.containsKey('bintray.key')) {

        println 'publish.properties found'
        bintray.user = props['bintray.user']
        bintray.key = props['bintray.key']

    } else {
        println 'publish.properties found but some entries are missing'
    }
} else {
    println 'publish.properties not found'
}
